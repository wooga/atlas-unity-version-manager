/*
 * Copyright 2018 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'base'
    id "org.ysb33r.rust.lib" version "0.2"
}

rust {
    executable version : '1.30.0'
    cargoToml './Cargo.toml'
    useCargoSourceLayout()
}

compileLibRust {
    options {
        release = true
        veryVerbose = true
    }
}

processRustSource {
    doLast {
        println("override Cargo.toml")
        copy {
            from projectDir
            include "Cargo.toml"
            into file("${buildDir}/rust-project")
        }
    }
}

configurations {
    rustLib
}

task copyOut(type:Copy) {
    dependsOn compileLibRust

    from file("${buildDir}/rust-project/target/release/")
    include "*.dylib"
    include "*.dll"
    into file("${buildDir}/output")
}

configurations['default'].extendsFrom(configurations.rustLib)

artifacts {
    rustLib file: file("${buildDir}/output/libuvm_jni.dylib"), name: "libuvm_extern", type: "dylib", builtBy: copyOut
    rustLib file: file("${buildDir}/output/libuvm_jni.dll"), name: "libuvm_extern", type: "dylib", builtBy: copyOut
}
